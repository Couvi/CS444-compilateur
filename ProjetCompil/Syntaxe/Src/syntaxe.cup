// ---------------------------------------------------------------------------
// syntaxe.cup
// Fichier cup qui definit l'analyseur syntaxique
// ---------------------------------------------------------------------------

package ProjetCompil.Syntaxe.Src;

import java_cup.runtime.*;
import ProjetCompil.Global.Src.*;
import java.io.InputStream;

/* Code pour l'analyseur syntaxique. */
parser code {:

   /**
    * Realise l'analyse syntaxique du fichier passe en argument (ou de 
    * l'entree standard si args est un tableau vide) et retourne 
    * l'abstrait correspondant.
    * Lève l'exception ErreurLexicale en cas d'erreur lexicale, et 
    * ErreurSyntaxe en cas d'erreur de syntaxe.
    */
   public static Arbre analyseSyntaxique(String[] args) 
         throws Exception, ErreurLexicale, ErreurSyntaxe {

      // On recupere le fichier a analyser
      InputStream fichierCas = ArgsFichier.ouvrir(args);

      // Creation de l'analyseur lexical
      Lexical analyseurLex = new Lexical(fichierCas);

      // Creation de l'analyseur syntaxique
      parser analyseurSynt = new parser(analyseurLex);

      // Appel de l'analyseur syntaxique et recuperation de l'arbre resultat
      Object result = analyseurSynt.parse().value;
      Arbre arbre = (Arbre) result;
      
      // On retourne l'arbre abstrait construit
      return arbre;
   }

   /**
    * Affiche un message d'erreur en cas d'erreur de syntaxe.
    */
   public void syntax_error(Symbol cur_token) {
      // Affichage du message d'erreur, avec le numero de ligne 
      System.err.println("Erreur de syntaxe ... ligne " + numLigne()) ;
   }

   /**
    * Methode appelee en cas d'erreur de syntaxe.
    */
   public void unrecovered_syntax_error(Symbol cur_token)
         throws ErreurSyntaxe {
      throw new ErreurSyntaxe();
   }

   /**
    * Retourne le numero de ligne courant.
    */
   public int numLigne() {
      // On recupere l'analyseur lexical
      Lexical anaLex = (Lexical) getScanner();
      return anaLex.numLigne();
   }
:};

/* Terminaux (tokens retournes par l'analyseur lexical. */
terminal String IDF;          // Identificateur
terminal Integer CONST_ENT;   // Constante entière
terminal Float CONST_REEL;    // Constante reelle
terminal String CONST_CHAINE; // Constante chaine

/* Mots reserves */
terminal   AND,         ARRAY,       BEGIN;
terminal   DIV,         DO,          DOWNTO;
terminal   ELSE,        END,         FOR;
terminal   IF,          MOD,         NEW_LINE;
terminal   NOT,         NULL,        OF;
terminal   OR,          PROGRAM,     READ;
terminal   THEN,        TO,          WHILE;
terminal   WRITE;

/* Operateurs */
terminal INF, SUP, EGAL, DIFF, // "<", ">", "=", "/="
         INF_EGAL, SUP_EGAL,   // "<=", ">="
         PLUS, MOINS,          // "+", "-"
         MULT, DIV_REEL;       // "*", "/"

/* Autres terminaux */
terminal PAR_OUVR, PAR_FERM; // "(", ")" 
terminal DOUBLE_POINT;       // ".."
terminal DEUX_POINTS;        // ":"
terminal VIRGULE;            // ","
terminal POINT_VIRGULE;      // ";"
terminal CROCH_OUVR;         // "["
terminal CROCH_FERM;         // "]"
terminal AFFECT;             // ":="
terminal POINT;              // "."

/* Definition des non terminaux de la grammaire */
non terminal Arbre program;
non terminal Arbre liste_decl;
non terminal Arbre liste_inst;
non terminal Arbre inst;
non terminal Arbre decl;
non terminal Arbre liste_idf;
non terminal Arbre type;
non terminal Arbre idf;
non terminal Arbre type_intervalle;
non terminal Arbre constante;
non terminal Arbre const;
non terminal Arbre pas;
non terminal Arbre liste_exp;
non terminal Arbre exp;
non terminal Arbre facteur;
non terminal Arbre place;

/*
   On definit les priorites des operateurs
   Les priorites vont dans l'ordre croissant
   On declare egalement l'associativite des operateurs
   (left, right, ou nonassoc)
*/

precedence nonassoc INF, SUP, EGAL, DIFF, INF_EGAL, SUP_EGAL;
precedence left PLUS, MOINS, OR;
precedence left MULT, DIV_REEL, DIV, MOD, AND;
precedence nonassoc NOT;

program ::= PROGRAM:p liste_decl:a1 BEGIN liste_inst:a2 END POINT
   {:
      RESULT = Arbre.creation2(Noeud.Programme, a1, a2, pleft);
      // pleft : Numero de ligne du mot reserve "program".
   :}
   ;
liste_decl ::= liste_decl:a1 decl:a2 POINT_VIRGULE
   {:
      RESULT = Arbre.creation2(Noeud.ListeDecl, a1, a2, a2.getNumLigne());
   :}
   |  // epsilon
   {:
      RESULT = Arbre.creation0(Noeud.Vide, parser.numLigne());
   :}
   ;

decl ::= liste_idf:a DEUX_POINTS type:b
   {:
      RESULT = Arbre.creation2(Noeud.Decl, a, b, parser.numLigne());
   :}
   ;
liste_idf ::= liste_idf:a VIRGULE idf:b
   {: 
      RESULT = Arbre.creation2(Noeud.ListeIdent, a, b, b.getNumLigne());
   :}
   |  idf:a
   {:
      RESULT = Arbre.creation2(Noeud.ListeIdent,
		Arbre.creation0(Noeud.Vide, parser.numLigne()), 
		a, aleft);
   :}
   ;

type ::= idf:a
   {:
      RESULT = a;
   :}
   |  type_intervalle:a
   {:
      RESULT = a;
   :}
   |  ARRAY CROCH_OUVR type_intervalle:a CROCH_FERM OF type:b
   {:
      RESULT = Arbre.creation2(Noeud.Tableau, a, b, a.getNumLigne());
   :}
   ;

type_intervalle ::= constante:a DOUBLE_POINT constante:b
   {:
     RESULT = Arbre.creation2(Noeud.Intervalle, a, b, a.getNumLigne());
   :}
   ;

constante ::= PLUS const:a
   {:
      RESULT = Arbre.creation1(Noeud.PlusUnaire, a, aleft);
   :}
   |  MOINS const:a
   {: 
      RESULT = Arbre.creation1(Noeud.MoinsUnaire, a, aleft);
   :}
   |  const:a
   {:
	  RESULT = a;
   :}
   ;

const ::= CONST_ENT:a
   {:
      RESULT = Arbre.creationEntier(a, aleft);
   :}
   |  idf:a
   {:
	  RESULT = a;
   :}
   ;
idf ::= IDF:a
   {:
      RESULT = Arbre.creationIdent(a, aleft);
   :}
   ;

liste_inst ::= liste_inst:a inst:b POINT_VIRGULE
   {: 
      RESULT = Arbre.creation2(Noeud.ListeInst, a, b, bleft);
   :}
   |  inst:a POINT_VIRGULE
   {:
      RESULT = Arbre.creation2(Noeud.ListeInst, 
		Arbre.creation0(Noeud.Vide, parser.numLigne()),
		a, aleft);
   :}
   ;

inst ::= NULL
   {:
	  RESULT = Arbre.creation0(Noeud.Nop, parser.numLigne());
   :}
   |  place:a AFFECT exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Affect, a, b, aleft);
   :}
   |  FOR pas:a DO liste_inst:b END
   {:
      RESULT = Arbre.creation2(Noeud.Pour, a, b, aleft);
   :}
   |  WHILE exp:a DO liste_inst:b END
   {:
      RESULT = Arbre.creation2(Noeud.TantQue, a, b, aleft);
   :}
   |  IF exp:a THEN liste_inst:b END
   {:
      RESULT = Arbre.creation3(Noeud.Si, a, b, 
      Arbre.creation2(Noeud.ListeInst, 
      Arbre.creation0(Noeud.Vide, parser.numLigne()),
      Arbre.creation0(Noeud.Nop, aleft), aleft), aleft);
   :}
   |  IF exp:a THEN liste_inst:b ELSE liste_inst:c END
   {:
      RESULT = Arbre.creation3(Noeud.Si, a, b, c, aleft);
   :}
   |  WRITE PAR_OUVR liste_exp:a PAR_FERM
   {:
      RESULT = Arbre.creation1(Noeud.Ecriture, a, aleft);
   :}
   |  READ PAR_OUVR place:a PAR_FERM
   {:
      RESULT = Arbre.creation1(Noeud.Lecture, a, aleft);
   :}
   |  NEW_LINE
   {:
      RESULT = Arbre.creation0(Noeud.Ligne,parser.numLigne());
   :}
   ;

pas ::= idf:a AFFECT exp:b TO exp:c
   {:
      RESULT = Arbre.creation3(Noeud.Increment, a, b, c, aleft);
   :}
   |  idf:a AFFECT exp:b DOWNTO exp:c
   {: 
      RESULT = Arbre.creation3(Noeud.Decrement, a, b, c, aleft);
   :}
   ;

liste_exp ::= liste_exp:a VIRGULE exp:b
   {:
      RESULT = Arbre.creation2(Noeud.ListeExp, a, b, aleft);
   :}
   |  exp:a
   {:
      RESULT = Arbre.creation2(Noeud.ListeExp, 
		Arbre.creation0(Noeud.Vide, parser.numLigne()),
		a, aleft);
   :}
   ;
   
exp ::= facteur:a
   {:
      RESULT = a;
   :}
   |  exp:a AND exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Et, a, b, aleft);
   :}
   |  exp:a OR exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Ou, a, b, aleft);
   :}
   |  exp:a EGAL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Egal, a, b, aleft);
   :}
   |  exp:a INF_EGAL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.InfEgal, a, b, aleft);
   :}
   |  exp:a SUP_EGAL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.SupEgal, a, b, aleft);
   :}
   |  exp:a DIFF exp:b
   {:
      RESULT = Arbre.creation2(Noeud.NonEgal, a, b, aleft);
   :}
   |  exp:a INF exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Inf, a, b, aleft);
   :}
   |  exp:a SUP exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Sup, a, b, aleft);
   :}
   |  exp:a PLUS exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Plus, a, b, aleft);
   :}
   |  exp:a MOINS exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Moins, a, b, aleft);
   :}
   |  exp:a MULT exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Mult, a, b, aleft);
   :}
   |  exp:a DIV_REEL exp:b
   {:
      RESULT = Arbre.creation2(Noeud.DivReel, a, b, aleft);
   :}
   |  exp:a MOD exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Reste, a, b, aleft);
   :}
   |  exp:a DIV exp:b
   {:
      RESULT = Arbre.creation2(Noeud.Quotient, a, b, aleft);
   :}
   |  PLUS facteur:a
   {: 
      RESULT = Arbre.creation1(Noeud.PlusUnaire, a, aleft);
   :}
   |  MOINS facteur:a
   {: 
      RESULT = Arbre.creation1(Noeud.MoinsUnaire, a, aleft);
   :}
   |  NOT facteur:a
   {: 
      RESULT = Arbre.creation1(Noeud.Non, a, aleft);
   :}
   ;

facteur ::= CONST_ENT:a
   {:
      RESULT = Arbre.creationEntier(a, aleft);
   :}
   |  CONST_REEL:a
   {: 
      RESULT = Arbre.creationReel(a, aleft);
   :}
   |  CONST_CHAINE:a
   {:
      RESULT = Arbre.creationChaine(a, aleft);
   :}
   |  place:a
   {:
      RESULT = a;
   :}
   |  PAR_OUVR exp:a PAR_FERM
   {:
      RESULT = a;
   :}
   ;

place ::= idf:a
   {:
      RESULT = a;
   :}
   |  place:a CROCH_OUVR exp:b CROCH_FERM
   {:
      RESULT = Arbre.creation2(Noeud.Index, a, b, b.getNumLigne());
   :}
   ;
